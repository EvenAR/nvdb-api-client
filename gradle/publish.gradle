
task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'nvdb-read-api-v3-client'

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            name = 'Release'
            url = "https://maven.triona.no/nexus/content/repositories/vegdata/"
        }
        maven {
            name = 'Snapshot'
            url = "https://maven.triona.no/nexus/content/repositories/vegdata-snapshot/"
        }
    }

}

if (project.hasProperty("mavenUser") && project.hasProperty("mavenPass")) {
    publishing {
        repositories.each {
            it.credentials {
                username mavenUser
                password mavenPass
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'vegdata'
        name = 'nvdb-api-client'
        userOrg = 'nvdb-vegdata'
        licenses = ['BSD 2-Clause']
        vcsUrl = 'https://github.com/nvdb-vegdata/nvdb-api-client'
        version {
            name = project.version
        }
    }
}
